plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'net.minecraftforge.gradle' version '[7.0.8,8)'
}

version = '1.0.0'
group = 'dev.fealtous.regionlib'

// Mojang ships Java 21 to end users in 1.20.5+, so your mod should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

// Include generated resources
sourceSets.main.resources { srcDir 'src/generated/resources' }

minecraft {
    mappings channel: 'official', version: '1.21.11'

    runs {
        configureEach {
            workingDir = layout.projectDirectory.dir('run')

            systemProperty 'eventbus.api.strictRuntimeChecks', 'true'
            systemProperty 'forge.enabledGameTestNamespaces', 'regionlib'
        }

        register('client') {
        }

        register('server') {
            args '--nogui'
        }

        register('gameTestServer')

        register('data') {
            workingDir = layout.projectDirectory.dir('run-data')

            args '--mod', 'regionlib', '--all', '--output', layout.projectDirectory.dir('src/generated/resources'), '--existing', layout.projectDirectory.dir('src/main/resources')
        }
    }
}

repositories {
    minecraft.mavenizer(it) // In Kotlin, it = this
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
    mavenCentral()
}

dependencies {
    implementation minecraft.dependency('net.minecraftforge:forge:1.21.11-61.1.1')
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0.1'
}

tasks.withType(JavaCompile).configureEach {
    // Use the UTF-8 charset for Java compilation
    // This is done by default in Java 18+, but this ensures it no matter the Java or Gradle version
    options.encoding = 'UTF-8'
}